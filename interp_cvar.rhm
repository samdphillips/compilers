#lang rhombus/static/and_meta

import:
  "cvar.rhm" open
  lib("racket/base.rkt"):
    only read
    expose read

export:
  rename interp_cvar_program as run

fun
| do_prim(#'#{+}, [a, b]): a + b
| do_prim(#'#{-}, [a]): - a
| do_prim(#'#{-}, [a, b]): a - b
| do_prim(#'read, []): read()

fun interp_atom(expr :: Atom, binds :: MutableMap):
  match expr
  | n :: Int: n
  | x :: Symbol:
      match binds[x]
      | #void: error("unbound var: " +& x)
      | v: v

fun interp_expr(expr :: Expr, binds :: MutableMap):
  match expr
  | _ :: Atom: interp_atom(expr, binds)
  | Prim(op, [arg, ...]):
      do_prim(op, [interp_atom(arg, binds), ...])

fun interp_cvar_stmt(stmt :: Stmt, binds :: MutableMap):
  match stmt
  | Assign(var, expr):
      binds[var] := interp_expr(expr, binds)

fun interp_cvar_section(insts :: Block, binds :: MutableMap):
  match insts
  | [Return(e)]:
      interp_expr(e, binds)
  | [stmt, &cont]:
      interp_cvar_stmt(stmt, binds)
      interp_cvar_section(cont, binds)

fun interp_cvar_program(pgm :: Program, label :: Symbol):
  def binds = MutableMap{}
  for (v: (pgm.info[#'vars] :~ Set)):
      binds[v] := #void
  interp_cvar_section(pgm.blocks[label], binds)
